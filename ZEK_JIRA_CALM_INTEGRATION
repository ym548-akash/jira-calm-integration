*&---------------------------------------------------------------------*
*& Report ZEK_JIRA_CALM_INTEGRATION
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT zek_jira_calm_integration.

*--- Input Parameters
PARAMETERS: p_jkey TYPE char20 OBLIGATORY, " Jira Issue Key
            p_proj TYPE string DEFAULT '811678b6-079b-4bff-a9c5-e802bf904b12'. " CALM Project GUID


*----------------------------------------------------------------------*
CLASS lcl_jira_to_calm DEFINITION.
  PUBLIC SECTION.
    METHODS execute IMPORTING iv_jira_key TYPE char20 iv_calm_proj TYPE string.
  PRIVATE SECTION.
    TYPES: BEGIN OF ty_jira_priority,
             id TYPE string,
           END OF ty_jira_priority,

           BEGIN OF ty_jira_status,
             name TYPE char40,
           END OF ty_jira_status,

           BEGIN OF ty_jira_fields,
             summary           TYPE string,
             created           TYPE string,
             duedate           TYPE string,
             description       TYPE string,
             priority          TYPE ty_jira_priority,
             customfield_12127 TYPE string,
             status            TYPE ty_jira_status,
           END OF ty_jira_fields,

           BEGIN OF ty_jira_issue,
             key    TYPE string,
             fields TYPE ty_jira_fields,
           END OF ty_jira_issue.

    TYPES: BEGIN OF ty_calm_payload,
             projectId   TYPE string,
             title       TYPE string,
             type        TYPE string,
             source      TYPE string,
             externalId  TYPE string,
             startDate   TYPE string,
             dueDate     TYPE string,
             priorityId  TYPE i,
             description TYPE string,
             workstream  TYPE string,
           END OF ty_calm_payload.

    TYPES: BEGIN OF ty_oauth_response,
             access_token TYPE string,
             token_type   TYPE string,
             expires_in   TYPE i,
           END OF ty_oauth_response.


    TYPES: BEGIN OF ty_calm_create_resp,
             id TYPE char50,
           END OF ty_calm_create_resp.

    DATA: gv_jira_status TYPE char40.

    METHODS fetch_jira_bug IMPORTING iv_key TYPE char20 RETURNING VALUE(rv_payload) TYPE string.
    METHODS transform_jira_to_calm
      IMPORTING iv_jira_payload        TYPE string
      RETURNING VALUE(rv_calm_payload) TYPE string.
    METHODS get_btp_token RETURNING VALUE(rv_token) TYPE string.
    METHODS create_calm_defect
      IMPORTING iv_token          TYPE string
                iv_payload        TYPE string
                iv_proj           TYPE string
      RETURNING VALUE(rv_calm_id) TYPE char50.
    METHODS update_log
      IMPORTING
        iv_jira_key    TYPE char20
        iv_calm_id     TYPE char50
        iv_jira_status TYPE char40.
    METHODS update_calm_status
      IMPORTING iv_token        TYPE string
                iv_calm_task_id TYPE char50.
ENDCLASS.

CLASS lcl_jira_to_calm IMPLEMENTATION.

  METHOD execute.

    "Fetch BTP Token for CALM API
    DATA(lv_token) = get_btp_token( ).

    "Fetch JIRA ticket
    DATA(lv_jira_payload) = fetch_jira_bug( iv_jira_key ).

    "Handle API failures
    IF lv_jira_payload = 'ERROR'.
      MESSAGE 'Failed to fetch data from JIRA' TYPE 'E'.
      RETURN.
    ENDIF.

    "Transform the payload for CALM API
    DATA(lv_calm_payload) = transform_jira_to_calm( iv_jira_payload = lv_jira_payload ).

    IF lv_token IS NOT INITIAL.
      "Fetch if the corresponding defect exists in CALM
      SELECT SINGLE * FROM zekt_jira_calm
      INTO @DATA(ls_log)
      WHERE jira_key = @iv_jira_key.

      IF sy-subrc IS NOT INITIAL.
        DATA(lv_calm_key) = create_calm_defect( iv_token = lv_token
                                                iv_payload = lv_calm_payload
                                                iv_proj = iv_calm_proj ).
        update_log(
            iv_jira_key    = iv_jira_key
            iv_calm_id     = lv_calm_key
            iv_jira_status = 'New' ).

      ELSE.

        update_calm_status( iv_token = lv_token
                            iv_calm_task_id = ls_log-calm_task_id ).

        update_log(
                    iv_jira_key    = iv_jira_key
                    iv_calm_id     = ls_log-calm_task_id
                    iv_jira_status = gv_jira_status ).

      ENDIF.

    ENDIF.
  ENDMETHOD.

  METHOD get_btp_token.
    " Implementation for: https://yamaha-motor-eu.authentication.eu10.hana.ondemand.com
    DATA: lo_http_client   TYPE REF TO if_http_client,
          lv_client_id     TYPE string VALUE 'sb-sap-cloud-alm-api!b599280|sapcloudalm-ext-01!b16907',
          lv_client_secret TYPE string VALUE 'add8197e-7fd7-4e52-b248-1c3b213605e7$b_BeJ3sJeA0MoFEeBLOyqiJKpRRZ_-UAYvbze6S6Jbk=',
          lv_body          TYPE string,
          lv_status        TYPE i,
          lv_response      TYPE string,
          ls_token         TYPE ty_oauth_response.
    cl_http_client=>create_by_url( EXPORTING url = 'https://yamaha-motor-eu.authentication.eu10.hana.ondemand.com/oauth/token'
                                   IMPORTING client = lo_http_client ).

    cl_http_client=>create_by_url(
   EXPORTING
     url    = 'https://yamaha-motor-eu.authentication.eu10.hana.ondemand.com/oauth/token'
   IMPORTING
     client = lo_http_client ).

    lo_http_client->request->set_method( if_http_request=>co_request_method_post ).
    lo_http_client->request->set_header_field(
      name  = 'Content-Type'
      value = 'application/x-www-form-urlencoded' ).

    lv_body =
      |grant_type=client_credentials&client_id={ lv_client_id }&client_secret={ lv_client_secret }|.

    lo_http_client->request->set_cdata( lv_body ).

    lo_http_client->send( ).
    lo_http_client->receive( ).

    lo_http_client->response->get_status( IMPORTING code = lv_status ).

    IF lv_status <> 200.
      RETURN.
    ENDIF.

    lv_response = lo_http_client->response->get_cdata( ).

    " Proper JSON parsing
    /ui2/cl_json=>deserialize(
      EXPORTING
        json = lv_response
      CHANGING
        data = ls_token ).

    rv_token = ls_token-access_token.

  ENDMETHOD.

  METHOD fetch_jira_bug.

    DATA: lo_http_client TYPE REF TO if_http_client,
          lv_url         TYPE string,
          lv_auth        TYPE string,
          lv_status      TYPE i.

    " Build Jira issue URL
    lv_url = |https://support.yamnet.com/rest/api/2/issue/{ iv_key }|.

    " Create HTTP client
    cl_http_client=>create_by_url(
      EXPORTING
        url                = lv_url
      proxy_host =  'proxy'
      proxy_service = '3128'
      IMPORTING
        client             = lo_http_client
      EXCEPTIONS
        argument_not_found = 1
        plugin_not_active  = 2
        internal_error     = 3
        OTHERS             = 4 ).

    IF sy-subrc <> 0.
      rv_payload = 'ERROR'.
      RETURN.
    ENDIF.

    " ---------- Authentication ----------
    lv_auth = cl_http_utility=>encode_base64( 'hchandra@yamaha-motor-india.com:Harshvardh@0o8' ).

    lo_http_client->request->set_header_field(
      name  = 'Authorization'
      value = |Basic { lv_auth }| ).

    lo_http_client->request->set_header_field(
      name  = 'Accept'
      value = 'application/json' ).

    lo_http_client->request->set_method( if_http_request=>co_request_method_get ).

    " ---------- Send Request ----------
    lo_http_client->send(
      EXCEPTIONS
        http_communication_failure = 1
        http_invalid_state         = 2
        http_processing_failed     = 3
        OTHERS                     = 4 ).

    IF sy-subrc <> 0.
      rv_payload = 'ERROR'.
      RETURN.
    ENDIF.

    " ---------- Receive Response ----------
    lo_http_client->receive(
      EXCEPTIONS
        http_communication_failure = 1
        http_invalid_state         = 2
        http_processing_failed     = 3
        OTHERS                     = 4 ).

    IF sy-subrc <> 0.
      rv_payload = 'ERROR'.
      RETURN.
    ENDIF.

    " ---------- Status Code ----------
    lo_http_client->response->get_status(
      IMPORTING
        code = lv_status ).

    IF lv_status <> 200.
*      rv_payload = |HTTP Error { lv_status }: { lo_http_client->response->get_cdata( ) }|.
      rv_payload = 'ERROR'.
      RETURN.
    ENDIF.

    " ---------- Response Payload ----------
    rv_payload = lo_http_client->response->get_cdata( ).

    " Cleanup
    lo_http_client->close( ).

  ENDMETHOD.

  METHOD transform_jira_to_calm.

    DATA: ls_jira     TYPE ty_jira_issue,
          ls_calm     TYPE ty_calm_payload,
          lv_priority TYPE i,
          lt_name_map TYPE /ui2/cl_json=>name_mappings.



    " 1. Deserialize Jira JSON
    /ui2/cl_json=>deserialize(
      EXPORTING
        json = iv_jira_payload
      CHANGING
        data = ls_jira ).

    " 2. Map fixed values
    ls_calm-projectId = p_proj.
    ls_calm-type      = 'CALMDEF'.
    ls_calm-source    = 'EXTERNAL'.

    " 3. Map direct fields
    ls_calm-title       = ls_jira-fields-summary.
    ls_calm-externalId  = ls_jira-key.
*    ls_calm-startDate   = ls_jira-fields-created.
*    ls_calm-dueDate     = ls_jira-fields-duedate.
    ls_calm-description = ls_jira-fields-description.
*    ls_calm-workstream  = ls_jira-fields-customfield_12127.

    " 4. Priority mapping
    CASE ls_jira-fields-priority-id.
      WHEN '10000' OR '10003'.
        ls_calm-priorityId = 10.
      WHEN '2'.
        ls_calm-priorityId = 20.
      WHEN '3'.
        ls_calm-priorityId = 30.
      WHEN '4'.
        ls_calm-priorityId = 40.
      WHEN OTHERS.
        ls_calm-priorityId = 30.
    ENDCASE.

    " 5. Prpare exact name mappings for payload
    lt_name_map = VALUE #(
        ( abap = 'PROJECTID'   json = 'projectId'   )
        ( abap = 'TITLE'       json = 'title'       )
        ( abap = 'TYPE'        json = 'type'        )
        ( abap = 'SOURCE'      json = 'source'      )
        ( abap = 'EXTERNALID'  json = 'externalId'  )
        ( abap = 'STARTDATE'   json = 'startDate'   )
        ( abap = 'DUEDATE'     json = 'dueDate'     )
        ( abap = 'PRIORITYID'  json = 'priorityId'  )
        ( abap = 'DESCRIPTION' json = 'description' )
        ( abap = 'WORKSTREAM'  json = 'workstream'  )
    ).


    " 6. Serialize CALM payload
    /ui2/cl_json=>serialize(
      EXPORTING
        data = ls_calm
        name_mappings = lt_name_map
      RECEIVING
        r_json = rv_calm_payload ).

    gv_jira_status = ls_jira-fields-status.

  ENDMETHOD.

  METHOD create_calm_defect.

    DATA: lo_http_client TYPE REF TO if_http_client,
          lv_status      TYPE i,
          lv_response    TYPE string,
          ls_resp        TYPE ty_calm_create_resp.

    " Create HTTP client
    cl_http_client=>create_by_url(
      EXPORTING
        url    = 'https://yamaha-motor-eu.eu10-004.alm.cloud.sap/api/calm-tasks/v1/tasks'
      IMPORTING
        client = lo_http_client ).

    " HTTP method
    lo_http_client->request->set_method( if_http_request=>co_request_method_post ).

    " Headers
    lo_http_client->request->set_header_field(
      name  = 'Authorization'
      value = |Bearer { iv_token }| ).

    lo_http_client->request->set_header_field(
      name  = 'Content-Type'
      value = 'application/json' ).

    lo_http_client->request->set_header_field(
      name  = 'x-project-id'
      value = iv_proj ).

    " Payload
    lo_http_client->request->set_cdata( iv_payload ).

    " Send request
    lo_http_client->send(
      EXCEPTIONS
        http_communication_failure = 1
        http_invalid_state         = 2
        http_processing_failed     = 3
        OTHERS                     = 4 ).

    IF sy-subrc <> 0.

    ENDIF.

    " Receive response
    lo_http_client->receive(
      EXCEPTIONS
        http_communication_failure = 1
        http_invalid_state         = 2
        http_processing_failed     = 3
        OTHERS                     = 4 ).

    IF sy-subrc <> 0.

    ENDIF.

    " Check HTTP status
    lo_http_client->response->get_status(
      IMPORTING
        code = lv_status ).

    lv_response = lo_http_client->response->get_cdata( ).

    IF lv_status = 200 OR lv_status = 201.

      /ui2/cl_json=>deserialize(
        EXPORTING
          json = lv_response
        CHANGING
          data = ls_resp
      ).

      rv_calm_id = ls_resp-id.
      MESSAGE 'Defect created successfuly!' TYPE 'S'.
    ELSE.
      MESSAGE 'Defect creation failed!' TYPE 'E'.
    ENDIF.

    lo_http_client->close( ).

  ENDMETHOD.

  METHOD update_log.
    DATA: ls_log TYPE zekt_jira_calm,
          lv_ts  TYPE timestampl.

    GET TIME STAMP FIELD lv_ts.

    SELECT SINGLE * FROM zekt_jira_calm
      INTO ls_log
      WHERE jira_key = iv_jira_key.

    IF sy-subrc = 0.
      ls_log-last_jira_status = iv_jira_status.
      ls_log-last_sync_ts     = lv_ts.
      MODIFY zekt_jira_calm FROM ls_log.
    ELSE.
      ls_log-mandt            = sy-mandt.
      ls_log-jira_key         = iv_jira_key.
      ls_log-calm_task_id     = iv_calm_id.
      ls_log-last_jira_status = iv_jira_status.
      ls_log-last_sync_ts     = lv_ts.
      INSERT zekt_jira_calm FROM ls_log.
    ENDIF.

    COMMIT WORK.

  ENDMETHOD.

  METHOD update_calm_status.

    DATA: lo_client    TYPE REF TO if_http_client,
          lv_url       TYPE string,
          lv_payload   TYPE string,
          lv_response  TYPE string,
          lv_substatus TYPE string,
          lv_status    TYPE i,
          lt_name_map TYPE /ui2/cl_json=>name_mappings.

    TYPES: BEGIN OF ty_calm_patch,
             subStatus TYPE string,
           END OF ty_calm_patch.

    DATA ls_payload TYPE ty_calm_patch.

    "-------------------------------
    " Map JIRA status â†’ CALM subStatus
    "-------------------------------
    CASE gv_jira_status.
      WHEN 'Open' OR 'New'.
        lv_substatus = 'DFC_NEW'.

      WHEN 'In Progress'.
        lv_substatus = 'DFC_INP'.

      WHEN 'Blocked' OR 'On Hold'.
        lv_substatus = 'DFC_POSTPONE'.

      WHEN 'Reopened' OR 'QA Failed'.
        lv_substatus = 'DFC_RETEST_REQ'.

      WHEN 'Done' OR 'Closed' OR 'Resolved'.
        lv_substatus = 'DFC_CLOSED'.

      WHEN OTHERS.
        RETURN.
    ENDCASE.

    "-------------------------------
    " Build JSON payload
    "-------------------------------
    ls_payload-subStatus = lv_substatus.

    lt_name_map = VALUE #(
        ( abap = 'SUBSTATUS'   json = 'subStatus'   )
    ).

    /ui2/cl_json=>serialize(
      EXPORTING
        data   = ls_payload
        name_mappings = lt_name_map
      RECEIVING
        r_json = lv_payload
    ).

    "-------------------------------
    " CALM PATCH URL
    "-------------------------------
    lv_url = |https://yamaha-motor-eu.eu10-004.alm.cloud.sap/api/calm-tasks/v1/tasks/{ iv_calm_task_id }|.

    cl_http_client=>create_by_url(
      EXPORTING
        url    = lv_url
      IMPORTING
        client = lo_client
    ).

    lo_client->request->set_method( 'PATCH' ).
    lo_client->request->set_header_field( name = 'Authorization' value = |Bearer { iv_token }| ).
    lo_client->request->set_header_field( name = 'Content-Type' value = 'application/json' ).
    lo_client->request->set_cdata( lv_payload ).

    lo_client->send( ).
    lo_client->receive( ).

    lo_client->response->get_status( IMPORTING code = lv_status ).
    lv_response = lo_client->response->get_cdata( ).

    " Optional: log failures
    IF lv_status <> 200 AND lv_status <> 204.
      MESSAGE 'Updation failed!' TYPE 'E'.
      RETURN.
    ENDIF.

  ENDMETHOD.


ENDCLASS.


START-OF-SELECTION.
  DATA(lo_sync) = NEW lcl_jira_to_calm( ).
  lo_sync->execute( iv_jira_key = p_jkey iv_calm_proj = p_proj ).
